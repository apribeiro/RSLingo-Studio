grammar rslingo.rslil.RSLIL with org.eclipse.xtext.common.Terminals

generate rSLIL "http://www.rslil.rslingo/RSLIL"

Model:
	// Business Level
	project=Project
	systems+=System*
	glossaryTerms+=GlossaryTerm*
	stakeholders+=Stakeholder*
	goals+=Goal*
	// System Level
	entities+=Entity*
	actors+=Actor*
	useCases+=UseCase*
	frs+=FR*
	nfrs+=NFR*;

Project:
	'Project' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' description=STRING
		('HasSystems' refSystem=RefSystem)?
	'}';

System:
	'System' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' description=STRING
		('PartOf' partOf=[System])?
	'}';

RefSystem:
	refSystem=[System](',' refs+=[System])*;

GlossaryTerm:
	'GlossaryTerm' name=ID '{'
		('Name' nameAlias=STRING)?
		'Type' type=RefTermType
		('Acronym' acronym=STRING)?
		'Description' description=STRING
		('POS' pos=('Adjective'|'Adverb'|'Noun'|'Verb'))?
		('Synset' synset=STRING)?
		termRelation+=TermRelation*
	'}';

RefTermType:
	refType=TermType(',' refs+=TermType)*;

TermType:
	type=('Stakeholder'|'Entity'|'Architectural'|'Actor');

TermRelation:
	type=('Antonym'|'Hypernym'|'Synonym') term=Term;

Term:
	refTerm=STRING(',' refs+=STRING)*;

Stakeholder:
	'Stakeholder' name=ID '{'
		('Name' nameAlias=STRING)?
		'Type' type=('Group.Organization'|'Group.BusinessUnit'|'Group.Team'|'Individual.Person'|'Individual.ExternalSystem')
		// FIXME Extend this list
		'Category' category=('Business.User.Direct'|'Business.User.Indirect'|'System.Engine')
		('Description' description=STRING)?
		('PartOf' partOf=[Stakeholder])?
	'}';

Goal:
	'Goal' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' description=STRING
		('Stakeholder' stakeholder=[Stakeholder])?
		'Priority' priority=('VeryLow'|'Low'|'Medium'|'High'|'VeryHigh')
		// TODO Lists
		dependsOn+=DependsOnGoal*
		composedBy+=ComposedBy*
	'}';

DependsOnGoal:
	type=('Requires'|'Supports'|'Obstructs'|'Conflicts'|'Identical') refGoal=[Goal];

ComposedBy:
	'ComposedBy' type=('Logic-And'|'Logic-Or') refGoal=[Goal];

Entity:
	'Entity' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' description=STRING
		attributes+=Attribute+
		// FIXME Define how to properly represent this relation
		(reference=Reference)?
	'}';

Attribute:
	'Attribute' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' descrition=STRING
		'Type' type=('Boolean'|'Integer'|'Decimal'|'Currency'|'Date'|'Time'|'Datetime'|'Enumeration'|'Text'|'Regex'|'URL'|'Image')
		field=Field
	'}';

Field:
	'Field' '{'
		'Size' size=Size
		'Multiplicity' multiplicity=('0'|'1'|'0..1'|'*'|STRING)
		('DefaultValue' defaultValue=STRING)?
		// TODO Constraints
	'}';

Size: INT | INT '.' INT;

Reference:
	'Reference' entity=[Entity] '(' refTo=RefAttribute ')' 'Multiplicity' multiplicity=('0'|'1'|'0..1'|'*'|STRING);

RefAttribute:
	refAttr=[Attribute](',' refs+=[Attribute])*;

Actor:
	'Actor' name=ID '{'
		('Name' nameAlias=STRING)?
		'Type' type=('User'|'ExternalSystem'|'Timer')
		'Description' description=STRING
		('Stakeholder' stakeholder=[Stakeholder])?
		// FIXME PartOf and SpecializedFrom?
		(dependsOn=DependsOnActor)?
	'}';

DependsOnActor:
	type=('PartOf'|'SpecializedFrom') actor=[Actor];

UseCase:
	'UseCase' name=ID '{'
		('Name' nameAlias=STRING)?
		'Type' type=('EntityManage'|'EntityBrowse'|'EntitySearch'|'EntityCreate'|'EntityDelete'|'EntitySync'|'Report')
		'Description' description=STRING
		'Priority' priotity=('VeryLow'|'Low'|'Medium'|'High'|'VeryHigh')
		extensionnPoints+=ExtensionPoint*
		('Goals' goals+=RefGoal*)?
		('FunctionalRequirements' frs+=RefFR*)?
		'ActorInitiates' actorInitiates=[Actor]
		('ActorParticipates' actors=RefActor)?
		('Pre-Conditions' preConditions=STRING)?
		('Post-Conditions' postConditions=STRING)?
		('Include' includes+=RefUC*)?
		// FIXME Review this concept!
		('Extend' extends+=[UseCase] 'on' extPoint=[ExtensionPoint])?
		scenarios+=Scenario*
	'}';

RefGoal:
	refGoal=[Goal](',' refs+=[Goal])*;

RefFR:
	refFR=[FR](',' refs+=[FR])*;

RefActor:
	refActor=[Actor](',' refs+=Actor)*;

RefUC:
	refUC=[UseCase](',' refs+=[UseCase])*;

ExtensionPoint:
	'ExtensionPoint' name=STRING;

Scenario:
	'Scenario' name=ID '{'
		('Name' nameAlias=STRING)?
		('Description' description=STRING)?
		'Type' type=('Main'|'Alternative'|'Exception')
		'ExecutionMode' mode=('Sequential'|'Parallel')
		steps+=Step*
	'}';

Step:
	'Step' name=ID '{'
		('Name' nameAlias=STRING)?
		'Type' type=('ActorPrepareData'|'ActorCallSystem'|'SystemExecutes'|'SystemReturnResult'|'Other')
		'Description' description=STRING
		('Actor' actor=[Actor])?
		('Pre-Conditions' preConditions=STRING)?
		('Post-Conditions' postConditions=STRING)?
		('NextStep' next=[Step])?
	'}';

FR:
	'FunctionalRequirement' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' description=STRING
		'Type' type=('Functional'|'Behavioral'|'Data')
		'Modality' modality=('Mandatory'|'Desirable'|'Optional')
		('Stakeholder' stakeholder=[Stakeholder])?
		'Priority' priority=('VeryLow'|'Low'|'Medium'|'High'|'VeryHigh')
		// FIXME Depends 1 or *?
		// Requires, Refines, Conflicts 
		('Depends' depends+=RefFR)?
		('PartOf' partOf=[FR])?
	'}';

NFR:
	'Non-FunctionalRequirement' name=ID '{'
		('Name' nameAlias=STRING)?
		'Description' description=STRING
		'Type' type=('Security'|'Performance'|'Usability'|'Appearance'|'Operational'|'Maintenance'|'Cultural'|'Legal')
		('Sub-Type' subType=('Security.Privacy'|'Security.Integrity'|'Usability.EaseOfUse'|'Usability.EaseOfLean'|'Usability.Accessibility'))?
		'Metric' metric=('Hours'|'Mins'|'Secs'|'mSecs'|'Tasks'|'Other')
		'Value' value=INT
		('Stakeholder' stakeholder=[Stakeholder])?
		'Priority' priority=('VeryLow'|'Low'|'Medium'|'High'|'VeryHigh')
		// Requires, Refines, Conflicts 
		('Depends' depends+=RefNFR)?
		('PartOf' partOf=[NFR])?
	'}';

RefNFR:
	refNFR=[NFR](',' refs+=[NFR])*;
