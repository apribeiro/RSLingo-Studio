grammar rslingo.rslil.RSLIL with org.eclipse.xtext.common.Terminals

generate rSLIL "http://www.rslil.rslingo/RSLIL"

Model:
	// Business Level
	project=Project
	systems+=System*
	glossaries+=Glossary*
	stakeholders+=Stakeholder*
	goals+=Goal*
	// System Level
	entities+=Entity*
	actors+=Actor*
	useCases+=UseCase*
	frs+=FR*
	nfrs+=NFR*;

Project:
	'Project' id=ID '{'
		'Name' name=STRING
		'Description' description=STRING
		('DecomposedBy' refSystem=RefSystem)?
	'}';

System:
	'System' id=ID '{'
		'Name' name=STRING
		'Description' description=STRING
		('PartOf' partOf=[System])?
	'}';

RefSystem:
	refSystem=[System](',' refs+=[System])*;
	
Glossary:
	'Glossary' id=ID '{'
		'Name' name=STRING
		'Type' type=RefGlossaryType
		'Acronym' acronym=STRING
		'Description' description=STRING
		('POS' pos=('Adjective'|'Adverb'|'Noun'|'Verb'))?
		('Synset' synset=STRING)?
		termRelation+=TermRelation*
	'}';

RefGlossaryType:
	refType=GlossaryType(',' refs+=GlossaryType)*;

GlossaryType:
	type=('Stakeholder'|'Entity'|'Architectural'|'Actor');

TermRelation:
	'TermRelation' type=('Antonym'|'Hypernym'|'Synonym') term=Term;

Term:
	refTerm=STRING(',' refs+=STRING)*;

Stakeholder:
	'Stakeholder' id=ID '{'
		'Name' name=STRING
		'Type' type=('Group.Organization'|'Group.BusinessUnit'|'Group.Team'|'Individual.Person'|'Individual.ExternalSystem')
		'Category' category=('Business.User.Direct'|'System.Engine')
		('Description' description=STRING)?
		('PartOf' partOf=[Stakeholder])?
	'}';

Goal:
	'Goal' id=ID '{'
		'Description' description=STRING
		'Stakeholder' stakeholder=[Stakeholder]
		'Priority' priority=('VeryLow'|'Low'|'Medium'|'High'|'VeryHigh')
		dependsOn+=DependsOnGoal*
		composedBy+=ComposedBy*
	'}';

DependsOnGoal:
	type=('Requires'|'Supports'|'Obstructs'|'Conflicts'|'Identical') refGoal=[Goal];

ComposedBy:
	'ComposedBy' type=('And'|'Or') refGoal=[Goal];

Entity:
	'Entity' id=ID '{'
		'Name' name=STRING
		'Description' description=STRING
		attributes+=Attribute+
	'}';

Attribute:
	'Attribute' '{'
		'Name' name=STRING
		'Description' descrition=STRING
		'Type' type=('Boolean'|'Integer'|'Decimal'|'Currency'|'Date'|'Time'|'Datetime'|'Enumeration'|'Text'|'Regex'|'Ref'|'Image')
		field=Field
		(reference=Reference)?
	'}';

Field:
	'Field' '{'
		'Size' size=INT
		'Multiplicity' multiplicity=('0'|'1'|'0..1'|'*'|STRING)
		'DefaultValue' defaultValue=STRING
	'}';

Reference:
	'Reference' refTo=RefAttribute 'Multiplicity' multiplicity=('0'|'1'|'0..1'|'*'|STRING);

RefAttribute:
	refAttr=[Attribute](',' refs+=[Attribute])*;

Actor:
	'Actor' id=ID '{'
		'Name' name=STRING
		'Type' type=('User'|'ExternalSystem'|'Timer')
		'Description' description=STRING
		('Stakeholder' stakeholder=[Stakeholder])?
		// FIXME PartOf and SpecializedFrom?
		(dependsOn=DependsOnActor)?
	'}';

DependsOnActor:
	type=('PartOf'|'SpecializedFrom') stakeholder=[Stakeholder];

UseCase:
	'UseCase' id=ID '{'
		'Name' name=STRING
		'Type' type=('EntityManage'|'EntityBrowse'|'EntitySearch'|'EntityCreate'|'EntityDelete'|'EntitySync'|'Report')
		'Description' description=STRING
		'Priority' priotity=('VeryLow'|'Low'|'Medium'|'High'|'VeryHigh')
		('Goals' goals+=RefGoal*)?
		('FunctionalRequirements' frs+=RefFR*)?
		'ActorInitiates' actorInitiates=[Actor]
		('ActorParticipates' actors=RefActor)?
		('Pre-Conditions' preConditions=STRING)?
		('Post-Conditions' postConditions=STRING)?
		('Include' includes+=RefUC*)?
		('Extend' extends+=[UseCase] 'using ExtensionPoint' extPoint=[ExtensionPoint])?
		extensionnPoints+=ExtensionPoint*
	'}';

RefGoal:
	refGoal=[Goal](',' refs+=[Goal])*;

RefFR:
	refFR=[FR](',' refs+=[FR])*;

RefActor:
	refActor=[Actor](',' refs+=Actor)*;

RefUC:
	refUC=[UseCase](',' refs+=[UseCase])*;

ExtensionPoint:
	'ExtensionPoint' name=STRING;

// TODO UseCase Scenarios

FR:
	'FunctionalRequirement' id=ID '{'
		'Name' name=STRING
		'Description' description=STRING
		'ModalityType' modalityType=STRING
		'ActionType' actionType=STRING
		'Stakeholder' stakeholder=[Stakeholder]
		'Priority' priority=STRING
		// depends / partof
	'}';

NFR:
	'Non-FunctionalRequirement' id=ID '{'
		'Name' name=STRING
		'Description' description=STRING
		'Type' type=STRING
		'Sub-Type' subType=STRING
		'Metric' metric=STRING
		'Value' value=STRING
		'Stakeholder' stakeholder=[Stakeholder]
		'Priority' priority=STRING
	'}';
